plugins {
    id 'java'
    id 'io.freefair.lombok' version "8.6"
    id 'org.jetbrains.kotlin.jvm'
    id 'com.github.johnrengelman.shadow' version "8.1.1"
}

group = 'dev.nafusoft'
version = '0.0.1'

shadowJar {
    archiveFileName.set("MagicTransportChest." + archiveExtension.get())
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        url = 'https://maven.aura-dev.team/repository/auradev-releases/'
    }
}

dependencies {
    compileOnly 'org.jetbrains:annotations:24.1.0'

    compileOnly 'org.spigotmc:spigot:1.20.1-R0.1-SNAPSHOT'

    implementation 'team.aura_dev.lib.slf4j-plugin.spigot:slf4j-plugin-spigot:1.2.0.39:1.7.25'
    implementation 'com.zaxxer:HikariCP:5.1.0'
    implementation 'com.mysql:mysql-connector-j:8.3.0'
    implementation 'org.mariadb.jdbc:mariadb-java-client:3.3.3'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    implementation 'redis.clients:jedis:5.1.2'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

kotlin {
    jvmToolchain(17)
}
